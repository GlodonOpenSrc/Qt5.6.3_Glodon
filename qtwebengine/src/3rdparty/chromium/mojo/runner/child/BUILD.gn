# Copyright 2015 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//mojo/public/mojo_application.gni")
import("//mojo/public/tools/bindings/mojom.gni")

group("child") {
  testonly = true
  deps = [
    ":apptests",
    ":lib",
  ]
}

source_set("lib") {
  sources = [
    "runner_connection.cc",
    "runner_connection.h",
  ]

  deps = [
    ":interfaces",
    "//base",
    "//mojo/message_pump",
    "//mojo/platform_handle:platform_handle_impl",
    "//mojo/shell/public/interfaces",
    "//third_party/mojo/src/mojo/edk/system",
  ]
}

mojom("interfaces") {
  sources = [
    "child_controller.mojom",
  ]

  deps = [
    "//mojo/shell/public/interfaces",
  ]

  import_dirs = [ "//mojo/services" ]
}

source_set("test_native_main") {
  sources = [
    "test_native_main.cc",
    "test_native_main.h",
  ]

  public_deps = [
    "//mojo/runner:init",
  ]

  deps = [
    "//base",
    "//mojo/gles2",
    "//mojo/message_pump",
    "//mojo/runner/child:lib",
    "//mojo/shell/public/cpp",
    "//third_party/mojo/src/mojo/edk/system",
  ]
}

mojom("apptest_interfaces") {
  sources = [
    "test_native_service.mojom",
  ]

  deps = [
    "//mojo/shell/public/interfaces",
  ]

  import_dirs = [ "//mojo/services" ]
}

mojo_native_application("apptests") {
  output_name = "mojo_runner_child_apptest"
  testonly = true
  avoid_runner_cycle = true

  sources = [
    "native_apptest.cc",
  ]

  deps = [
    ":apptest_interfaces",
    "//base",
    "//base/test:test_config",
    "//mojo/common:common_base",
    "//mojo/converters/network:network",
    "//mojo/shell/public/cpp:sources",
    "//mojo/shell/public/cpp:test_support",
  ]

  data_deps = [
    ":native_target",
  ]
}

executable("native_target") {
  output_name = "mojo_runner_child_apptest_native_target"
  testonly = true

  sources = [
    "native_apptest_target.cc",
  ]

  deps = [
    ":apptest_interfaces",
    ":lib",
    ":test_native_main",
    "//base",
    "//build/config/sanitizers:deps",
    "//mojo/common:common_base",
    "//mojo/shell/public/cpp",
  ]
}
