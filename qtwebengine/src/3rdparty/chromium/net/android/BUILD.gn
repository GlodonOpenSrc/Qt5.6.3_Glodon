# Copyright 2014 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/android/config.gni")
import("//build/config/android/rules.gni")

android_library("net_java") {
  DEPRECATED_java_in_dir = "java/src"
  deps = [
    "//base:base_java",
  ]
  srcjar_deps = [
    ":net_errors_java",
    ":net_android_java_enums_srcjar",
  ]
}

android_aidl("embedded_test_server_aidl") {
  interface_file = "../test/android/javatests/src/org/chromium/net/test/IEmbeddedTestServerInterface.aidl"
  sources = [
    "../test/android/javatests/src/org/chromium/net/test/IEmbeddedTestServerImpl.aidl",
  ]
}

android_library("net_java_test_support") {
  testonly = true
  DEPRECATED_java_in_dir = "../test/android/javatests/src"
  deps = [
    ":net_java",
    "//base:base_java",
    "//base:base_java_test_support",
    "//third_party/android_tools:legacy_http_javalib",
  ]
  srcjar_deps = [
    ":embedded_test_server_aidl",
    ":net_java_test_support_enums_srcjar",
  ]
}

source_set("java_test_native_support") {
  testonly = true
  sources = [
    "../test/android/net_test_entry_point.cc",
    "../test/android/net_test_jni_onload.cc",
    "../test/android/net_test_jni_onload.h",
    "../test/embedded_test_server/android/embedded_test_server_android.cc",
    "../test/embedded_test_server/android/embedded_test_server_android.h",
  ]

  deps = [
    "//net:test_support",
  ]
  public_deps = [
    "//net:net_test_jni_headers",
  ]
}

shared_library("net_java_test_native_support") {
  testonly = true
  deps = [
    ":java_test_native_support",
    "//net:test_support",
  ]
}

android_apk("net_test_support_apk") {
  testonly = true
  deps = [
    ":net_java_test_native_support",
    ":net_java_test_support",
    "//base:base_java",
  ]
  android_manifest = "../test/android/javatests/AndroidManifest.xml"
  apk_name = "ChromiumNetTestSupport"
  native_libs = [ "libnet_java_test_native_support.so" ]
}

android_resources("net_unittests_apk_resources") {
  resource_dirs = [ "unittest_support/res" ]
  custom_package = "org.chromium.native_test"
}

android_library("net_javatests") {
  testonly = true
  DEPRECATED_java_in_dir = "javatests/src"
  deps = [
    ":net_java",
    ":net_java_test_support",
    "//base:base_java",
    "//base:base_java_test_support",
  ]
}

java_cpp_template("net_errors_java") {
  package_name = "org/chromium/net"
  sources = [
    "java/NetError.template",
  ]
  inputs = [
    "../base/net_error_list.h",
  ]
}

java_cpp_enum("net_java_test_support_enums_srcjar") {
  sources = [
    "../test/url_request/url_request_failed_job.h",
  ]
}

java_cpp_enum("net_android_java_enums_srcjar") {
  sources = [
    "../base/mime_util.h",
    "../base/network_change_notifier.h",
    "cert_verify_result_android.h",
    "keystore.h",
    "network_change_notifier_android.cc",
    "traffic_stats.cc",
  ]
}

junit_binary("net_junit_tests") {
  java_files =
      [ "junit/src/org/chromium/net/HttpNegotiateAuthenticatorTest.java" ]
  deps = [
    ":net_java",
    "//base:base_java",
    "//base:base_java_test_support",
    "//base:base_junit_test_support",
    "//third_party/junit:hamcrest",
  ]
}
